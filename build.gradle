buildscript {
    ext {
        kotlin_version = '1.3.21'
        dokka_version = '0.9.17'
        jvm_version = '1.8'
        detekt_version = '1.0.0-RC14'
        // Version to publish
        watchable_version = '0.5.0'
    }

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url = 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

plugins {
    id "com.github.hierynomus.license" version "0.15.0"
    id "io.gitlab.arturbosch.detekt" version "1.0.0-RC14"
}

apply plugin: 'kotlin'
apply plugin: 'java-library'
apply plugin: 'jacoco'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'io.gitlab.arturbosch.detekt'
apply plugin: 'maven-publish'
apply plugin: 'signing'

version = ext.watchable_version
group = 'com.gladed'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    // Transitively supply all kotlin APIs to our users
    api "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Coroutines are deeply integrated here
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.1'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.15.0'

    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$detekt_version"
}

targetCompatibility = 1.8
sourceCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

compileKotlin.kotlinOptions {
    allWarningsAsErrors = true
    jvmTarget = "$ext.jvm_version"
    freeCompilerArgs += ["-Xuse-experimental=kotlin.Experimental"]
}

compileTestKotlin.kotlinOptions {
    jvmTarget = "$ext.jvm_version"
    freeCompilerArgs += ["-Xuse-experimental=kotlin.Experimental"]
}

test {
    testLogging {
        events "failed"
        showExceptions = true
        exceptionFormat = "full"
    }
}

test {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/model/**'])
        })
    }
}

detekt {
    input = files("$projectDir/src/main")
    baseline = file("$projectDir/src/config/baseline.xml")
}

dokka {
    outputDirectory = "$buildDir/javadoc"
    outputFormat = "javadoc"
    externalDocumentationLink {
        url = new URL("http://docs.oracle.com/javase/8/docs/api/")
    }
}

license {
    skipExistingHeaders = true
    header = rootProject.file('src/config/header')
    mapping("fxml", "XML_STYLE")
    excludes(["**/*.md"])
}

task sourceJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = "javadoc"
    from dokka.outputDirectory
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

signing {
    required { gradle.taskGraph.hasTask("publish") }
    sign configurations.archives
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId group
            artifactId project.name

            from components.java

            // create the sign pom artifact
            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]

                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            artifact(sourceJar) {
                classifier = 'sources'
            }

            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            // create the signed artifacts
            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                    } else {
                        classifier = null
                    }
                    extension = 'jar.asc'
                }
            }
        }
    }

    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username project.ext.properties.sonatypeUsername
                password project.ext.properties.sonatypePassword
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = DELEGATE_FIRST

            description 'Collections that may be watched for changes'
            name project.name
            url 'https://github.com/gladed/watchable'
            organization {
                name 'Glade Diviney'
                url 'https://github.com/gladed'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/HPInc/jipp/issues'
            }
            licenses {
                license {
                    name 'Apache 2.0'
                    url 'https://github.com/gladed/watchable/blob/master/LICENSE.md'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/gladed/watchable'
                connection 'scm:git:git://github.com/gladed/watchable.git'
                developerConnection 'scm:git:ssh://git@github.com:gladed/watchable.git'
            }
            developers {
                developer {
                    name 'Glade Diviney'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }

    tasks.publishMavenJavaPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }

    tasks.publishMavenJavaPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
}
